---
title: "Fishシェルでターミナルのタイトルをプロジェクトルートからの相対パスっぽくする"
author: atusy
date: '2025-01-26'
slug: fish-title-relative-to-projroot
categories: [Tech]
tags: [fish]
output:
  blogdown::html_page:
    md_extensions: +east_asian_line_breaks+task_lists
highlighhtjs: [fish]
summary: |
  たとえば、git管理しているプロジェクトとかで、`foo/app`とかにするイメージ
---



<p>Fishシェルを使いだしてから半年以上が経過しました。重厚な設定をしなくてもそれなりに使えて、起動も十分高速なことから、愛用しています。たまにBashコマンドのコピペしたい時は<code>bash -c '...'</code>するなり一時的に<code>bash</code>を起動すればいいので特に困ってもいません。</p>
<blockquote>
<p><a href="https://blog.atusy.net/2024/05/27/fish/">2024-5-27 fish使い始めた</a></p>
</blockquote>
<p>ところでFishシェルは<code>fish_title</code>関数を設定しておくと、コマンドの実行前・後に関数が自動的に評価され、ターミナルのタイトルを更新できます。仮想端末ウィンドウのタイトルバーやタブバーの中身を動的に変更できる便利な関数です。</p>
<blockquote>
<p>fish_title - define the terminal’s title<br />
<a href="https://fishshell.com/docs/current/cmds/fish_title.html" class="uri">https://fishshell.com/docs/current/cmds/fish_title.html</a></p>
</blockquote>
<p>カレントディレクトリの名前を表示してくれれば十分なケースが多いのですが、モノリポの場合など、プロジェクト内で作業ディレクトリを変更する場合には、プロジェクトルートからの相対パスが欲しくなるので、自前で実装してみました。</p>
<p>素朴にはこんな感じ。</p>
<pre class="fish"><code># `config.fish`に書いてもいいが、
# ~/.config/private_fish/functions/fish_title.fish
# に記述しておくと、遅延ロードされる
function fish_title
  set -l wd
  if set -l gwd (git rev-parse --show-toplevel 2&gt; /dev/null)
    set -l n (dirname &quot;xx$gwd&quot; | string length) # add extra characters to generate start index
    set wd (string sub --start $n $PWD)
  else
    set wd (basename $PWD)
  end

  echo -- $wd
end</code></pre>
<p>実用している<code>fish_title</code>はもう少し工夫しています。</p>
<ul>
<li>カレントディレクトリに変更があった時のみ<code>git rev-parse --show-toplevel</code>を評価
<ul>
<li><code>fish_title</code>関数はコマンドの実行前と実行後に繰り返し評価されるため、結果をキャッシュ</li>
</ul></li>
<li>SSH中かどうか表示</li>
<li>デフォルト設定にあったEmacsがらみの条件判定を拝借
<ul>
<li>今後Emacsを使いたくなった時用</li>
</ul></li>
</ul>
<pre class="fish"><code>set -l _fish_title_pwd
set -l _fish_title_wd

function fish_title
  if not set -q INSIDE_EMACS; or string match -vq &#39;*,term:*&#39; -- $INSIDE_EMACS
    set -l ssh
    set -q SSH_TTY
    and set ssh &quot;[&quot;(prompt_hostname | string sub -l 10 | string collect)&quot;]&quot;

    if test &quot;$_fish_title_pwd&quot; != &quot;$PWD&quot;
      set _fish_title_pwd $PWD
      if set -l gwd (git rev-parse --show-toplevel 2&gt; /dev/null)
        set -l n (dirname &quot;xx$gwd&quot; | string length) # add extra characters to generate start index
        set _fish_title_wd (string sub --start $n $PWD)
      else
        set _fish_title_wd (basename $PWD)
      end
    end

    echo -- $ssh $_fish_title_wd
  end
end</code></pre>
<div id="enjoy" class="section level1">
<h1>ENJOY!</h1>
</div>
